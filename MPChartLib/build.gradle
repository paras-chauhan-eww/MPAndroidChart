plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'kotlin-android'
    id "com.vanniktech.maven.publish" version "0.29.0"
}

ext {
    mGroupId = "info.appdevnext"
    mArtifactId = "chart"
    mVersionName = getTag()

    mLibraryName = "Android Chart"
    mLibraryDescription = "A powerful Android chart view / graph view library, supporting line- bar- pie- radar- bubble- and candlestick charts as well as scaling, dragging and animations"
}

android {
    namespace 'com.github.mikephil.charting'
    defaultConfig {
        minSdkVersion 21
        compileSdk 34
        targetSdkVersion 34

        // VERSION_NAME no longer available as of 4.1
        // https://issuetracker.google.com/issues/158695880
        buildConfigField 'String', 'VERSION_NAME', "\"${getTag()}\""

        consumerProguardFiles 'proguard-project.txt'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    buildFeatures {
        buildConfig = true
    }
    testOptions {
        unitTests.returnDefaultValues = true // this prevents "not mocked" error
    }
}

dependencies {
    implementation 'androidx.annotation:annotation:1.8.2'
    testImplementation 'junit:junit:4.13.2'
}

tasks.register('androidSourcesJar', Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}
